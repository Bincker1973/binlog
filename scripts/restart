#!/bin/env expect
set workdir "~/"
set server [lindex $argv 0]
set jar_file "binlog-0.0.1-SNAPSHOT.jar"
set java_path "/opt/jdk-15/bin/java"

if { $server=="" } {
    puts "使用方法： $argv0 user@server"
    exit 1
}

expect_after {
    timeout {
        puts "等待超时"
        exit 1
    }
    eof {
        puts "断开连接"
        exit 1
    }
}

send_user "password: "
stty -echo
expect_user {
    -timeout 600
    -re "(.*)\n" {
        set password $expect_out(1,string)
        send_user "\n"
    }
}
stty echo

spawn ssh $server
expect {
    "password: " {
        send "$password\r"
        exp_continue
    }
    "(yes/no)?" {
        send "yes\r"
        exp_continue
    }
    fail {
        puts "连接失败"
        exit 1
    }
    -re "\\\$|# " {
        puts "登录成功"
    }
}

proc expect_complete {} {
    expect {
        -timeout 300
        -re "\\\$|# " {}
    }
}

send "cd $workdir\r"
expect_complete
puts "查找进程"
send "ps aux | grep \" -jar $jar_file\"\r"
set process_regexp "\\w+\\s+(\\d+)\\s+\\S+\\s+\\S+\\s+\\S+\\s+\\S+\\s+\\S+\\s+\\S+\\s+\\S+\\s+\\S+\\s+$java_path -jar $jar_file"
expect {
    -timeout 300
    -re $process_regexp {
        puts "发现进程"
        puts $expect_out(0,string)
        set pid $expect_out(1,string)
        expect_complete
        send "kill $pid\r"
        expect_complete
        sleep 1
    }
    -re "\\\$|# " {}
}
# 等待进程结束
send "ps aux | grep \" -jar $jar_file\"\r"
expect {
    -re $process_regexp {
        sleep 1
        exp_continue
    }
    -re "\\\$|# " {}
}
puts "启动应用..."
send "nohup $java_path -jar $jar_file > nohup.out &\r"
sleep 10
send "\r"
expect_complete
while 1 {
    send "tail -n 20 nohup.out\r"
    expect {
        -timeout 60
        "Application run failed" {
            expect_complete
            puts $expect_out(buffer)
            send "exit\r"
            expect eof
            exit 1
        }
        "Web server failed to start" {
            expect_complete
            puts $expect_out(buffer)
            send "exit\r"
            expect eof
            puts "启动失败"
            exit 1
        }
        -re "Started \\w+ in \\S+ seconds" {
            puts "重启成功"
            expect_complete
            send "exit\r"
            expect eof
            exit
        }
        -re "\\\$|# " {
            puts $expect_out(buffer)
            sleep 1
            exp_continue
        }
    }
}
